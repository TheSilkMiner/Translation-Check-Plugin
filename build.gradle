buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.3'
    }
}

plugins {
    id 'java'
    id 'eclipse'
    id 'idea'
    //id 'checkstyle'
    id 'maven'
    id 'groovy'
    id 'java-gradle-plugin'
    id 'com.github.johnrengelman.shadow' version '1.2.3'
}

//noinspection GroovyUnusedAssignment
sourceCompatibility = 1.8
//noinspection GroovyUnusedAssignment
targetCompatibility = 1.8

version = pluginVersion
group = "net.thesilkminer.gradle.translationchecker"
//noinspection GroovyUnusedAssignment
archivesBaseName = "Translation-Check-Plugin"

repositories {
    mavenCentral()
    jcenter()
}

//noinspection GroovyAssignabilityCheck,GroovyAssignabilityCheck
configurations {
    shade
	pack
    compile.extendsFrom shade
	compile.extendsFrom pack
}

dependencies {
    shade 'net.sf.jopt-simple:jopt-simple:5.0-beta-1'
    shade 'org.codehaus.groovy:groovy:2.4.4'
    compile gradleApi()
	compile project(":SKL-Java-Interpreter")
	shade project(":SKL-Java-Interpreter")
	pack project(":SKL-Java-Interpreter")
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

ext.jarManifest = manifest {
	attributes(
		"Archive-Type": "jar"
	)
}

ext.zipManifest = manifest {
	attributes(
		"Archive-Type": "zip"
	)
}

ext.unarchivedManifest = manifest {
	attributes(
		"Archive-Type": "unarchived"
	)
}

ext.authorManifest = manifest {
	attributes(
		"Authors": "TheSilkMiner, boq"
	)
}

ext.runnableManifest = manifest {
	attributes(
		"Main-Class": "net.thesilkminer.gradle.plugin.translationchecker.Standalone"
	)
}

ext.gitManifest = manifest {
	attributes(
		"Travis-Build": "false"
	)
}

ext.shadowManifest = manifest {
	attributes (
		"ShadowJar": "true"
	)
}

ext.normalManifest = manifest {
	attributes (
		"ShadowJar": "false"
	)
}

ext.sourcesManifest = manifest {
	attributes(
		"__comment": "This archive only contains sources: you still need to download the main archive to use/develop with the plugin"
	)
}

// sort of an hack but it works
// this is the minimal jar...
jar {
	classifier = 'minimal'
    manifest {
        from runnableManifest, gitManifest, jarManifest, authorManifest, normalManifest
    }
}

shadowJar {
    configurations = [ project.configurations.shade ]
    exclude '**/*.html'
	manifest {
		from runnableManifest, gitManifest, jarManifest, authorManifest, shadowManifest
	}
}

//noinspection GroovyAssignabilityCheck
build.dependsOn shadowJar

// Hacky way to separate java and groovy sources but let them compile
// anyway (thank you GroovyC because you can compile Java classes)
sourceSets.main.java.srcDirs = []
sourceSets.main.groovy.srcDirs += ["src/main/java"]

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
	manifest {
		from gitManifest, jarManifest, authorManifest, normalManifest, sourcesManifest
	}
}

// ... and this is the jar you should use.
task normalJar(type: Jar, dependsOn: classes) {
	manifest {
        from runnableManifest, gitManifest, jarManifest, authorManifest, normalManifest
    }
	from sourceSets.main.output
	configurations.pack.each { dependency ->
		from(project.zipTree(dependency)){
            exclude 'META-INF', 'META-INF/**'
        }
	}
}

artifacts {
    archives sourcesJar
	archives normalJar
}

task wrapper (type: Wrapper) {
    gradleVersion = "2.11"
}
